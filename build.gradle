import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java-library'
    id 'distribution'
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
}

group 'org.citygml4j'
version '1.2.0'
description 'A Gson based library for parsing and serializing CityJSON'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
    withJavadocJar()
    withSourcesJar()
}

ext {
    date = new Date()
}

repositories {
    mavenCentral()
}

dependencies {
    api 'com.google.code.gson:gson:2.11.0'
}

javadoc {
    options {
        overview "$projectDir/build/tmp/javadoc/overview.html"
        header "citygson $project.version"
        bottom """
            <a target="_blank" href="https://github.com/citygml4j/citygson/issues">Report a bug or suggest an enhancement</a><br>
            citygson is open source and licensed under the <a target="_blank" href="http://www.apache.org/licenses/LICENSE-2.0">Apache License, Version 2.0</a><br>
            Official website and more information at: <a target="_blank" href="https://github.com/citygml4j/citygson">https://github.com/citygml4j/citygson</a><br>
            Copyright &copy; 2013-${date.format('yyyy')}<br>
            Claus Nagel &lt;claus.nagel@gmail.com&gt;
            """.replaceAll("[\r|\n]+", "")
        addStringOption('doctitle', project.name + ' - ' + project.description)
    }

    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }

    doFirst {
        copy {
            from 'resources/javadoc/overview.html'
            into "$projectDir/build/tmp/javadoc"
            filter(ReplaceTokens, tokens: [
                    version: project.version
            ])
        }
    }
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
    into('META-INF') {
        from 'LICENSE'
    }
}

distributions.main.contents {
    from('resources/doc/README') {
        filter(ReplaceTokens, tokens: [
                version: project.version,
                date: date.format('yyyy-MM-dd'),
                copyright: date.format('yyyy'),
                javaCompatibility: '11',
                jar: jar.archiveFileName.get(),
                groupId: project.group,
                artifactId: project.name
        ])
    }
    into('lib') {
        from jar
        from configurations.compileClasspath
    }
    into('javadoc') {
        from javadoc
    }
    into('license') {
        from 'LICENSE'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = project.name
                description = project.description
                url = 'https://github.com/citygml4j/citygson'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'clausnagel'
                        name = 'Claus Nagel'
                        email = 'claus.nagel@gmail.com'
                        roles = ['architect', 'developer']
                        timezone = '+1'
                    }
                }
                scm {
                    url = 'https://github.com/citygml4j/citygson'
                    connection = 'scm:git:https://github.com/citygml4j/citygson.git'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

nexusPublishing {
    repositories {
        sonatype()
    }
}